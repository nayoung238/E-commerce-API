plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2024.0.0")
}

jacoco {
    toolVersion = '0.8.12'
}

subprojects {
    apply plugin: 'java'

    dependencies {
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.3'
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

jacocoTestReport {
    reports {
        html.required.set(true)
        xml.required.set(true)
        csv.required.set(false)
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, include: [
                            '**/application/**',
                            '**/api/**',
                            '**/service/**',
                            '**/entity/**',
                            '**/repository/**'
                    ], exclude: [
                            '**/dto/**',
                            '**/common/**',
                            '**/security/**',
                            '**/validation/**'
                    ])
                })
        )
    }
}